plugins {
    id 'application'
}

// utility function
static def path(final String ...s) {
    return String.join(File.separator, s)
}

String fatJarBaseName = "${appNameLowerCase}-readelf"
String fatJarName = "${fatJarBaseName}.jar"
String fatJarPath = path("${project.layout.buildDirectory.get().toString()}", "libs", "${fatJarName}")
String mainClassName = "${basePackage}.readelf.Main"

dependencies {
    implementation project(':elf')
    implementation project(':utils')
}

application {
    mainClass = mainClassName
}

run {
    args = [
            // "/usr/bin/gcc"
    ]
    jvmArgs = [
            // "-ea",
            // "-esa",
            // "-XX:+TieredCompilation",
            // "-XX:+PrintCompilation"
    ]
}

// utility function
static def getDate() {
    return new Date().format('yyyyMMddHHmmss')
}

tasks.register('fatJar', Jar) {
    group 'distribution'
    description 'Builds the fat jar of the application'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    duplicatesStrategy = 'include'
    manifest {
        archiveBaseName = appNameLowerCase
        attributes(
                // "Implementation-Title": "Gradle",
                // "Implementation-Version": version,
                "Main-Class": "$mainClassName"
        )
    }
    archiveBaseName = "${fatJarBaseName}"

    doLast {
        printf "The fat jar is %s\n", fatJarPath
        printf "Fat jar size: %.3f MB\n", new File(fatJarPath).length() / 1_000_000L
    }
}

tasks.build.dependsOn(fatJar)
tasks.javadoc.enabled = false

if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {

    // Utility task to check that the output of readelf is the same as GNU's readelf
    task checkReadelf {
        dependsOn fatJar

        doLast {
            final List<String> executables = List.of(
                "gcc",
                "g++",
                "clang",
                "clang++",
                "zip",
                "unzip"
            );

            for(final String elf : executables) {
                final String filepath = "/usr/bin/" + elf
                if (!(new File(filepath).exists())) {
                    continue
                }

                println("Checking $filepath with system readelf")
                exec {
                    commandLine "readelf", "-a", "${filepath}"
                    standardOutput = new FileOutputStream("${elf}_all_ref.out")
                }
                exec {
                    commandLine "readelf", "-a", "-W", "${filepath}"
                    standardOutput = new FileOutputStream("${elf}_all_wide_ref.out")
                }

                println("Checking $filepath with custom readelf")
                exec {
                    commandLine "java", "-jar", "${fatJarPath}", "readelf", "-a", "${filepath}"
                    standardOutput = new FileOutputStream("${elf}_all.out")
                }
                exec {
                    commandLine "java", "-jar", "${fatJarPath}", "readelf", "-a", "-W", "${filepath}"
                    standardOutput = new FileOutputStream("${elf}_all_wide.out")
                }
            }
        }
    }
}
