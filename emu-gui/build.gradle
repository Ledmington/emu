plugins {
	id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

// utility function
static def path(final String ...s) {
	return String.join(File.separator, s);
}

String emuGUIVersion = "0.1.0"
String fatJarBaseName = "${appNameLowerCase}-gui-${emuGUIVersion}"
String fatJarName = "${fatJarBaseName}.jar"
String fatJarPath = path("${project.layout.buildDirectory.get().toString()}", "libs", "${fatJarName}")
String mainClassName = "${basePackage}.emu.Launcher"

dependencies {
    implementation project(':emu')
    implementation project(':elf')
    implementation project(':id')
    implementation project(':mem')
    implementation project(':utils')
}

application {
	mainClass = mainClassName
}

run {
	args = []
	jvmArgs = []
}

javafx {
	version = '21.0.4'
	modules = [ 'javafx.controls', 'javafx.graphics' ]
}

tasks.register('fatJar', Jar) {
	group 'distribution'
	description 'Builds the fat jar of the application'
	from sourceSets.main.output
	dependsOn configurations.runtimeClasspath
	from {
		configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
	}
	duplicatesStrategy = 'include'
	manifest {
		archiveBaseName = appNameLowerCase
		attributes(
				// "Implementation-Title": "Gradle",
				"Implementation-Version": "${emuGUIVersion}",
				"Main-Class": "$mainClassName"
		)
	}
	archiveBaseName = "${fatJarBaseName}"

	doLast {
		printf "The fat jar is %s\n", fatJarPath
		printf "Fat jar size: %.3f MB\n", new File(fatJarPath).length() / 1_000_000L
	}
}

tasks.build.dependsOn(fatJar)
tasks.javadoc.enabled = false
