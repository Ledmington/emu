plugins {
    id 'application'
}

// utility function
static def path(final String ...s) {
    return String.join(File.separator, s)
}

String objdumpVersion = "0.1.0"
String fatJarBaseName = "${appNameLowerCase}-objdump-${objdumpVersion}"
String fatJarName = "${fatJarBaseName}.jar"
String fatJarPath = path("${project.layout.buildDirectory.get().toString()}", "libs", "${fatJarName}")
String mainClassName = "${basePackage}.objdump.Main"

dependencies {
    implementation project(':id')
    implementation project(':elf')
    implementation project(':utils')
}

application {
    mainClass = mainClassName
}

run {
    args = []
    jvmArgs = []
}

tasks.register('fatJar', Jar) {
    group = 'distribution'
    description = 'Builds the fat jar of the application'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        archiveBaseName = appNameLowerCase
        attributes(
                // "Implementation-Title": "Gradle",
                "Implementation-Version": "${objdumpVersion}",
                "Main-Class": "$mainClassName"
        )
    }
    archiveBaseName = "${fatJarBaseName}"

    doLast {
        printf "The fat jar is %s\n", fatJarPath
        printf "Fat jar size: %.3f MB\n", new File(fatJarPath).length() / 1_000_000L
    }
}

tasks.build.dependsOn(fatJar)
tasks.javadoc.enabled = false

tasks.register('checkObjdump', JavaExec) {
    group = 'verification'
    description = 'Checks the objdump output against the one provided by the system'
    dependsOn fatJar
    classpath sourceSets.test.runtimeClasspath
    mainClass = 'com.ledmington.objdump.CheckObjdump'
}
