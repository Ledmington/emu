plugins {
	id 'java'
	id 'application'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'com.adarshr.test-logger' version '4.0.0'
	id 'com.github.ben-manes.versions' version '0.51.0'
	id 'de.undercouch.download' version '5.5.0'
}

repositories {
	mavenCentral()
}

static def path(String... args) {
	return String.join(File.separator, args)
}

ext.junitVersion = "5.10.2"
dependencies {
	testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

application {
	mainClass = 'com.ledmington.emu.Main'
	applicationDefaultJvmArgs = [
			"-Xms2G",
			"-Xmx2G",
			// "-XX:+PrintCompilation",
			//"-XX:+PrintInlining",
			//"-XX:+LogCompilation",
			//"-XX:LogFile=jvm_compilation_log"
	]
}

run {
	args = ["/usr/bin/gcc"]
}

test {
	useJUnitPlatform()
}

testlogger {
	theme 'standard'
	// theme 'mocha'
	// theme 'mocha-parallel'

	showExceptions true
	showStackTraces true
	showFullStackTraces false
	showCauses true

	slowThreshold 2000

	showSummary true
	showSimpleNames false
	showPassed false
	showSkipped true
	showFailed true
	showOnlySlow false

	showStandardStreams true
	showPassedStandardStreams false
	showSkippedStandardStreams true
	showFailedStandardStreams true

	logLevel 'lifecycle'
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
		setEncoding('utf-8')
	}

	java {
		target fileTree('.') {
			include path('**', '*.java')
			exclude path('**', 'build', '**'), path('**', 'build-*', '**'), path('**', 'bin', '**')
		}
		removeUnusedImports()
		formatAnnotations()
		trimTrailingWhitespace()
		endWithNewline()
		setEncoding('utf-8')
		palantirJavaFormat('2.39.0')

		// check https://javadoc.io/doc/com.diffplug.spotless/spotless-plugin-gradle/latest/com/diffplug/gradle/spotless/JavaExtension.html
		importOrder('java', 'javax', 'javafx', 'com', 'org', group as String)
	}
}

tasks.compileJava.dependsOn(spotlessApply)

tasks.register('downloadManual', Download) {
	group 'Utilities'
	description 'Downloads the Intel Software Developer Manual in the project root directory'
	src "https://cdrdv2-public.intel.com/812392/325462-sdm-vol-1-2abcd-3abcd-4.pdf"
	dest new File(project.getRootDir(), "intel-developer-manual.pdf")
	overwrite false
	acceptAnyCertificate true
}
