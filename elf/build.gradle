plugins {
	id 'java-library'
	id 'info.solidsoft.pitest' version '1.19.0-rc.1'
	id 'me.champeau.jmh' version '0.7.3'
}

dependencies {
	implementation project(':utils')
}

pitest {
	targetClasses.set(['com.ledmington.elf.*'])
	targetTests.set(['com.ledmington.elf.*'])
	pitestVersion = '1.22.0'
	junit5PluginVersion = '1.2.3'
	threads = 4
	outputFormats.set(['XML', 'HTML'])
	useClasspathFile = true     // useful with bigger projects on Windows
	verbose = true
	timestampedReports = false
}

jmh {
	includes = ['com.ledmington.elf.*'] // include pattern (regular expression) for benchmarks to be executed
	fork = 2 // How many times to fork a single benchmark. Use 0 to disable forking altogether.
	failOnError = true // Should JMH fail immediately if any benchmark had experienced the unrecoverable error?
	forceGC = false // Should JMH force GC between iterations?
	jvmArgs = []
	resultsFile = project.file("${project.layout.buildDirectory.get()}/reports/jmh/results.txt")
	resultFormat = 'TEXT'
	threads = 1
	verbosity = 'EXTRA'
	zip64 = true // Use ZIP64 format for bigger archives
	jmhVersion = '1.37'
	duplicateClassesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register("jmhDryRun", JavaExec) {
	group = "verification"
	description = "JMH dry-run (runs benchmarks only 1 time)."
	dependsOn jmhJar
	mainClass = "-jar"
	systemProperty "jmh.ignoreLock", "true"
	args = [
		tasks.named("jmhJar").get().archiveFile.get().asFile.absolutePath,
		"com.ledmington.elf.*",
		"-f", "1",
		"-wi", "0",
		"-i", "1",
		"-rf", "TEXT"
	]
}

tasks.build.dependsOn(jmhJar)
tasks.build.dependsOn(jmhDryRun)

tasks.register('downloadELF32Manual', Download) {
	group = 'utilities'
	description = 'Downloads the ELF 32-bit file format specification in the project root directory'
	src "https://refspecs.linuxfoundation.org/elf/elf.pdf"
	dest new File(project.getRootDir(), "elf32-manual.pdf")
	overwrite false
	acceptAnyCertificate true
}

tasks.register('downloadELF64Manual', Download) {
	group = 'utilities'
	description = 'Downloads the ELF 64-bit file format specification in the project root directory'
	src "https://uclibc.org/docs/elf-64-gen.pdf"
	dest new File(project.getRootDir(), "elf64-manual.pdf")
	overwrite false
	acceptAnyCertificate true
}
