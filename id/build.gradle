plugins {
	id 'java-library'
	id 'info.solidsoft.pitest' version '1.19.0-rc.1'
	id 'me.champeau.jmh' version '0.7.3'
}

dependencies {
	implementation project(':utils')
}

pitest {
	targetClasses.set(['com.ledmington.cpu.x86.*'])
	targetTests.set(['com.ledmington.cpu.x86.*'])
	pitestVersion = '1.22.0'
	junit5PluginVersion = '1.2.3'
	threads = 4
	outputFormats.set(['XML', 'HTML'])
	useClasspathFile = true     // useful with bigger projects on Windows
	verbose = true
	timestampedReports = false
}

jmh {
	includes = ['com.ledmington.id.*'] // include pattern (regular expression) for benchmarks to be executed
	fork = 2 // How many times to fork a single benchmark. Use 0 to disable forking altogether.
	failOnError = true // Should JMH fail immediately if any benchmark had experienced the unrecoverable error?
	forceGC = false // Should JMH force GC between iterations?
	jvmArgs = []
	resultsFile = project.file("${project.layout.buildDirectory.get()}/reports/jmh/results.txt")
	resultFormat = 'TEXT'
	threads = 1
	verbosity = 'EXTRA'
	zip64 = true // Use ZIP64 format for bigger archives
	jmhVersion = '1.37'
	duplicateClassesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.register("jmhDryRun", JavaExec) {
	group = "verification"
	description = "JMH dry-run (runs benchmarks only 1 time)."
	dependsOn jmhJar
	mainClass = "-jar"
	systemProperty "jmh.ignoreLock", "true"
	args = [
		tasks.named("jmhJar").get().archiveFile.get().asFile.absolutePath,
		"com.ledmington.id.*",
		"-f", "1",
		"-wi", "0",
		"-i", "1",
		"-rf", "TEXT"
	]
}

tasks.build.dependsOn(jmhJar)
tasks.build.dependsOn(jmhDryRun)

tasks.register('downloadIntelManual', Download) {
	group = 'utilities'
	description = 'Downloads the Intel Software Developer Manual in the project root directory'
	src "https://cdrdv2-public.intel.com/812392/325462-sdm-vol-1-2abcd-3abcd-4.pdf"
	dest new File(project.getRootDir(), "intel-developer-manual.pdf")
	overwrite false
	acceptAnyCertificate true
}

tasks.register('fuzz', JavaExec) {
	group = 'verification'
	description = 'Generates random x86_64 instructions to catch unexpected errors'
	dependsOn classes
	classpath sourceSets.test.runtimeClasspath
	mainClass = 'com.ledmington.cpu.x86.X86Fuzzer'
}
